Goal -> MainClass ClassDeclarations .
MainClass -> class id lbrace void main ( string lbracket rbracket id ) lbrace Blockstatements rbrace rbrace .
Blockstatements -> BlockStatement Blockstatements .
Blockstatements -> .
BlockStatement -> id AfterId semicolon .
BlockStatement -> NonclassVarDec semicolon .
BlockStatement -> StmtWithoutId .
AfterId -> id Eq . 
AfterId -> AfterIdExceptId .
Eq -> eq E .
Eq -> .
NonclassVarDec -> NonclassType id Eq .
Type1 -> lbracket rbracket Type1 .
Type1 -> .
NonclassType -> boolean Type1 .
NonclassType -> int Type1 .
NonclassType -> void .
Type -> NonclassType .
Type -> id Type1 .
Stmt -> id AfterIdExceptId semicolon .
Stmt -> StmtWithoutId .
StmtWithoutId -> lbrace Blockstatements rbrace .
StmtWithoutId -> while ( E ) Stmt .
StmtWithoutId -> systemoutprintln ( E ) semicolon .
StmtWithoutId -> continue semicolon .
StmtWithoutId -> break semicolon .
StmtWithoutId -> return OptExp semicolon .
StmtWithoutId -> if ( E ) Stmt OptElse .
StmtWithoutId -> semicolon .
StmtWithoutId -> this dot id AfterThisInStmt .
OptElse -> else Stmt | .
OptExp -> E .
OptExp -> .
AfterThisInStmt -> AfterIdExceptId .
AfterThisInStmt -> ( T3 ) OptAfterIdExceptId .
OptAfterIdExceptId -> AfterIdExceptId .
OptAfterIdExceptId -> .
AfterIdExceptId -> Dot Eq .
AfterIdExceptId -> Bracket Eq .
AfterIdExceptId -> eq E .
Dot -> dot id ( T3 ) DotR .
DotR -> dot id ( T3 ) DotR .
DotR -> lbracket E rbracket DotR .
DotR -> .
Bracket -> lbracket BracketR .
BracketR -> E rbracket DotR .
BracketR -> rbracket BracketEmpty id .
BracketEmpty -> lbracket rbracket BracketEmpty .
BracketEmpty -> .
ClassDeclarations -> ClassDeclaration ClassDeclarations .
ClassDeclarations -> .
ClassDeclaration -> class id Extends ClassBody .
Extends -> extends id .
Extends -> .
ClassBody -> lbrace ClassContent rbrace .
ClassContent -> ClassComponent ClassContent .
ClassContent -> .
ClassComponent -> Type id RestDec .
RestDec -> Eq semicolon .
RestDec -> ( ParamsOpt ) lbrace Blockstatements rbrace .
ParamsOpt -> Params .
ParamsOpt -> .
Params -> Param ParamsRest .
ParamsRest -> comma Params .
ParamsRest -> .
Param -> Type id .
E -> E1 Ep .
Ep -> Relop E1 Ep .
Ep -> .
E1 -> E2 E1p .
E1p -> Boolop E2 E1p .
E1p -> .
E2 -> E3 E2p .
E2p -> Addop E3 E2p .
E2p -> .
E3 -> T E3p .
E3p -> Multop T E3p .
E3p -> .
T -> F Tp .
T -> Unop T .
T -> this AfterThisInExp .
Tp -> X Tp .
Tp -> .
T2 -> comma E .                   
T2 -> .
T3 -> E T4 .
T3 -> .
T4 -> comma E T4 .
T4 -> .
X -> dot Xp .
X -> lbracket E rbracket .
Xp -> length P .
Xp -> substring ( E T2 ) .
Xp -> id ( T3 ) .
P -> ( ) .
P -> .
F -> TT .
F -> R .
TT -> litint .
TT -> true .
TT -> false .
TT -> id .
TT -> litstr .
TT -> null .
TT -> new AfterNew .
AfterThisInExp -> dot id RestThisInExp .
AfterThisInExp -> .
RestThisInExp -> ( T3 ) Tp .
RestThisInExp -> .
AfterNew -> int FilledBrackets .
AfterNew -> boolean FilledBrackets .
AfterNew -> id AfterNewId .
AfterNewId -> lbracket E rbracket FilledBrackets . 
AfterNewId -> ( T3 ) .
FilledBrackets -> lbracket E rbracket FilledBrackets .
FilledBrackets -> .
R -> lbrace El rbrace .
R -> ( E ) .
El -> E Elp .
El -> .
Elp -> comma E Elp .
Elp -> .
Relop -> less .
Relop -> lesseq .
Relop -> greatereq .
Relop -> greater .
Relop -> isequal .
Relop -> isdiff .
Boolop -> and .
Boolop -> or .
Addop -> plus .
Addop -> minus .
Multop -> times .
Multop -> div .
Multop -> mod .
Unop -> minus .
Unop -> excl .